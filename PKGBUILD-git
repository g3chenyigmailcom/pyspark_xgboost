# Maintainer: Julian Qian <julian.qian@gmail.com>
pkgbase=python-pyspark-xgboost-git
pkgname=('python-pyspark-xgboost-git' 'python2-pyspark-xgboost-git')
_pyname=pyspark-xgboost
_gitname=pyspark-xgboost
pkgver=20170101
pkgrel=1
pkgdesc='pyspark xgboost'
arch=('any')
url='https://github.com/jq/pyspark-xgboost'
license=('BSD')
makedepends=('git' 'python' 'python2' 'python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("git+https://github.com/jq/${_gitname}")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${_gitname}"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-py2"
}

package_python-pyspark-xgboost-git() {
  depends=('python' 'python-setuptools')
  provides=('python-pyspark-xgboost')
  conflicts=('python-pyspark-xgboost')
  cd "${srcdir}/${_gitname}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_python2-pyspark-xgboost-git() {
  depends=('python2' 'python2-setuptools')
  provides=('python2-pyspark-xgboost')
  conflicts=('python2-pyspark-xgboost')
  cd "${srcdir}/${_gitname}-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

# vim:set ts=2 sw=2 et:
